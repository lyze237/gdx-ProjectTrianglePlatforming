buildscript {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        gradlePluginPortal()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.3.0'
}

apply from: 'utils.gradle'
group = 'dev.lyze'
version = "1.0.${getGitHash()}"
apply from: 'publishing.gradle'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    xmlLibs
}

dependencies {
    implementation "com.github.lyze237:gdx-Clipper2:$gdxClipper2Version"

    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"

    implementation "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    testImplementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"

    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
    sourceCompatibility = 8
    targetCompatibility = 8
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
        showStackTraces = true
        showExceptions = true
        showCauses = true
        displayGranularity = 0
        events = [
                "standardOut",
                "standardError",
                "started",
                "passed",
                "skipped",
                "failed"
        ]
        showStandardStreams = true
    }

    reports {
        junitXml.configure {
            enabled(true)
        }
    }

    if (project.hasProperty("lwjgl")) {
        useJUnitPlatform {
            includeTags "lwjgl"
        }
    } else {
        useJUnitPlatform {
            excludeTags 'lwjgl'
        }
    }
}

compileJava {
    options.incremental = true
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
